apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = jacocoTestLimit
            }
        }
    }
}

if (project.hasProperty('jacocoExclusion') && jacocoExclusion != null && !jacocoExclusion.isEmpty() ) {
    [jacocoTestCoverageVerification, jacocoTestReport].each { item ->
        afterEvaluate {
            item.classDirectories.from = files(item.classDirectories.files.collect {
                fileTree(
                        dir: it,
                        exclude: jacocoExclusion)
            })
        }
    }
}

ext.jacocoTasks = { Task... tests ->
    jacocoTestReport.executionData(tests)
    jacocoTestCoverageVerification.executionData(tests)
    check.dependsOn jacocoTestReport
    jacocoTestReport.mustRunAfter tests
    check.dependsOn jacocoTestCoverageVerification
    jacocoTestCoverageVerification.mustRunAfter jacocoTestReport
}

